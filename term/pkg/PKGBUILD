pkgname=st
pkgver=0.6
pkgrel=1
pkgdesc='Simple virtual terminal emulator for X'
url='http://git.suckless.org/st/'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft')
source=(http://dl.suckless.org/st/st-${pkgver}.tar.gz
        config.h)
md5sums=('1a926f450b4eacb7e2f5ac5b8ffea7c8'
         '2ae89e1b5d3541bd9b383041e81f1fbf')

prepare() {
    cd ${srcdir}/${pkgname}-${pkgver}
    cp ${srcdir}/config.h config.h
    sed \
        -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
        -e 's/CFLAGS =/CFLAGS +=/g' \
        -e 's/LDFLAGS =/LDFLAGS +=/g' \
        -e 's/_BSD_SOURCE/_DEFAULT_SOURCE/' \
        -i config.mk
    sed '/@tic/d' -i Makefile
}

build() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
    cd ${srcdir}/${pkgname}-${pkgver}
    make PREFIX=/usr DESTDIR="${pkgdir}" install
}

