pkgname=dwm-custom-ah
pkgver=6.1
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama' 'libxft' 'freetype2')
source=(http://dl.suckless.org/dwm/dwm-${pkgver}.tar.gz
        config.h
        01-dwm-6.1-ansicolors.diff)
md5sums=('f0b6b1093b7207f89c2a90b848c008ec'
         '90790761ab89d7f30c8f9412fa261e53'
         '0caa29a29bdec3581a744aa01a5c18d7')

prepare() {
    cd ${srcdir}/dwm-${pkgver}
    cp ${srcdir}/config.h .
    cp ${srcdir}/01-dwm-6.1-ansicolors.diff .
    patch -p1 < 01-dwm-6.1-ansicolors.diff
    sed \
        -e 's/CPPFLAGS =/CPPFLAGS +=/g' \
        -e 's/CFLAGS   = -std/CFLAGS   += -std/g' \
        -e 's/LDFLAGS =/LDFLAGS +=/g' \
        -e 's/_BSD_SOURCE/_DEFAULT_SOURCE/' \
        -i config.mk
    sed '/@tic/d' -i Makefile
}

build() {
    cd ${srcdir}/dwm-${pkgver}
    make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11 FREETYPEINC=/usr/include/freetype2
}

package() {
    cd ${srcdir}/dwm-${pkgver}
    make PREFIX=/usr DESTDIR=${pkgdir} install
}

