pkgname=dwm-ah-git
_pkgname=dwm
pkgver=20151020.646b351
pkgrel=1
pkgdesc="A dynamic window manager for X"
url="http://dwm.suckless.org"
arch=('i686' 'x86_64')
license=('MIT')
options=(zipman)
depends=('libx11' 'libxinerama')
makedepends=('git')
source=('git://git.suckless.org/dwm'
	'config.h'
	'01-dwm-646b351-statuscolors.diff')
md5sums=('SKIP'
         '88deb1b2d32a2380d7f36e471e535863'
         'd1b0e1820b253849202db7ab6a337f1a')

provides=("${_pkgname}")
conflicts=("${_pkgname}")

pkgver (){
	cd ${_pkgname}
	git log -1 --format='%cd.%h' --date=short | tr -d -
}

prepare() {
	cd ${srcdir}/${_pkgname}
	cp ${srcdir}/config.h .
	cp ${srcdir}/01-dwm-646b351-statuscolors.diff .
	patch -p1 < 01-dwm-646b351-statuscolors.diff
	sed \
		-e 's/CPPFLAGS =/CPPFLAGS +=/g' \
		-e 's/CFLAGS   = -std/CFLAGS   += -std/g' \
		-e 's/LDFLAGS =/LDFLAGS +=/g' \
		-e 's/_BSD_SOURCE/_DEFAULT_SOURCE/' \
		-i config.mk
	sed '/@tic/d' -i Makefile
}

build() {
	cd ${srcdir}/${_pkgname}
	make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
	cd ${srcdir}/${_pkgname}
	make PREFIX=/usr DESTDIR=${pkgdir} install
}

